;; ##### SETUP FUNCTIONS ####

to load
  set projection "WGS_84_Geographic"
  set parking-dataset gis:load-dataset "GIS_data/parking.geojson"
  set residential-dataset gis:load-dataset "GIS_data/residential.geojson"
  set grass-dataset gis:load-dataset "GIS_data/grass.geojson"
  set houses-dataset gis:load-dataset "GIS_data/houses.geojson"
  set station-dataset gis:load-dataset "GIS_data/Ede-station.geojson"
  gis:set-world-envelope (list 5.6716 5.6860 52.0280 52.0206)
end

to setup-spots
  ;; For each parking space, determine the centroid, convert to x,y coordinates (list with 2 elements),
  ;; and create a turtle in the center of that polygon
  foreach gis:feature-list-of parking-dataset [ this-vector-feature ->
    let center gis:centroid-of this-vector-feature
    let loc gis:location-of center

    if length loc >= 2 [
      create-spots 1 [
        setxy item 0 loc item 1 loc
        set capacity gis:property-value this-vector-feature "capacity"
        ifelse capacity != "" [set capacity read-from-string capacity][die] ;; TODO: Fix spots without capacity
        set shape "square"
        set color red
        set size 0.1
        set label capacity
        set private? false
      ]
    ]
  ]
end

to setup-station
  ask patches [set station? false]
  foreach gis:feature-list-of station-dataset [ this-vector-feature ->
    let center gis:centroid-of this-vector-feature
    let loc gis:location-of center

    if length loc >= 2 [
      ask patch item 0 loc item 1 loc [set pcolor yellow set station? true]
    ]
  ]
end

to setup-households
  foreach gis:feature-list-of houses-dataset [ this-vector-feature ->
    let center gis:centroid-of this-vector-feature
    let loc gis:location-of center

    if length loc >= 2 [setup-household loc]
    ]

  ask n-of 10 households [type "Household ages: " ask residents with [household-nr = myself] [type age type ", "] type "child wish: " print child-wish]
end

to setup-household [loc]
  create-households 1 [
    setxy item 0 loc item 1 loc
    set driveway first rnd:weighted-one-of-list [[0 0.8] [1 0.15] [2 0.05]] [[p] -> last p]
    set shape "house"
    set color blue
    set size 0.1
    set distance-spot distance min-one-of spots [distance myself]
    set distance-station distance one-of patches with [station?]

    setup-residents
  ]
end

to setup-residents
  ;; Create between 1 and 2 adults with an age within a few years of eachother
  let intial-adults first rnd:weighted-one-of-list [[1 0.1] [2 0.9]] [[p] -> last p]
  let approximate-adults-age 25 + random 41
  hatch-residents intial-adults [
    set parent? true
    set age approximate-adults-age + random 4 - 3
    set color yellow
    set-resident-properties
    setup-cars
  ]

  ;; Determine their child wish and the approximate age of their childeren (between 20 and 30 years younger)
  set child-wish first rnd:weighted-one-of-list [[0 0.2] [1 0.35] [2 0.35] [3 0.1]] [[p] -> last p]
  let approximate-child-age approximate-adults-age - 20 - random 11

  ;; For each child, determine if they either A) have already moved out, B) are not born yet or C) are living with them, and adjust the child wish accordingly
  foreach range child-wish [
    let predicted-child-age approximate-child-age + random 6 - 4
    if predicted-child-age > 0 [ ;; if born
      set child-wish child-wish - 1
      if predicted-child-age < (18 + random 8) [ ;; if also still living at home
        hatch-residents 1 [
          set parent? false
          set age predicted-child-age
          set color orange
          set-resident-properties
          setup-cars
        ]
      ]
    ]
  ]
end

to set-resident-properties
  set size 0.3
  set heading random 360
  set household-nr myself
  
  set initial-modality-perference table:make
  table:put initial-modality-perference "car" random-float 1
  table:put initial-modality-perference "bike" random-float 1
  table:put initial-modality-perference "public-transport" random-float 1
  table:put initial-modality-perference "shared-car" random-float 1
end

to setup-cars
  let gets-car? false ;; initialize variable
  if age > 18 [
    ifelse parent?
      [set gets-car? random-float 1 < initial-car-chance-parent / 100]
      [set gets-car? random-float 1 < initial-car-chance-child / 100]
  ]
  ifelse gets-car?
    [set owns-car? true
     hatch-cars 1 [set-car-properties]
    ]
    [set owns-car? false]
end

to set-car-properties
  set owner myself
  set shared? false
  set lease? random-float 1 < 0.12  ;; CBS
  ifelse lease?
    [set age random 4
     set mileage age * (9000 + random 18000)]
    [set age random 10
     set mileage age * (5000 + random 10000)]
  ;; TODO: Think about (yearly) costs

  set shape "car"
  set size 0.5
  let target-spot min-one-of spots with [not private? and occupancy < capacity] [distance myself]
  move-to target-spot
  ask target-spot [set occupancy occupancy + 1]
end
;;[owner shared? age yearly-costs km-costs mileage lease? in-use?]

to draw
  gis:set-drawing-color grey
  gis:fill residential-dataset 0
  gis:set-drawing-color green
  gis:fill grass-dataset 0
  gis:set-drawing-color red
  gis:fill parking-dataset 0
  gis:set-drawing-color blue
  gis:fill houses-dataset 0
end

to setup-contacts
  print "Setting up contacts:"
  ;; Setup neighbours
  ask residents [
    let n-neighbours random-poisson average-neighbour-contacts  ;; Note that poisson might not be the ideal distribution
    set neighbours-contacts up-to-n-of n-neighbours residents in-radius 3
    ;; TODO: Maybe consider age
  ]

  ;; Setup contacts for parents with childeren of same age
  ask residents [set parent-contacts nobody]
  ask residents with [parent?] [
    ;; Get ages of own childeren
    let childeren residents with [household-nr = [household-nr] of myself and not parent? and age <= 12]
    let ages sort [age] of childeren

    ask childeren [
      let peers residents with [abs(age - [age] of myself) <= 1] ;; Max one year age difference
      let peer-parents residents with [parent? and member? household-nr [household-nr] of peers]
      let n-peers random-poisson average-parent-contacts-per-child
      if n-peers >= 1 [
        let new-parents-contacts up-to-n-of n-peers peer-parents
        set parent-contacts (turtle-set parent-contacts new-parents-contacts)
      ]
    ]
  ]
  ask n-of 10 residents [type "Neighbours: " type count neighbours-contacts type ", parent contacts: " type parent-contacts type ", total: " print count (turtle-set neighbours-contacts parent-contacts)]
end

to setup-destinations
  ask residents [
    set work-destinations n-values 1 [create-destination true]
    set other-destinations n-values 3 [create-destination false]
  ]
end

to-report create-destination [work?]
  let trip-distance 0
  let trip-length 0
  ifelse work? [
    set trip-distance random-gamma (mean-distance-work * mean-distance-work / variance-distance-work) (1 / (variance-distance-work / mean-distance-work))
    set trip-length work-trip-length
  ] [
    set trip-distance random-gamma (mean-distance-other * mean-distance-other / variance-distance-other) (1 / (variance-distance-other / mean-distance-other))
    set trip-length other-trip-length
  ]

  let destination-table table:make
  table:put destination-table "car-time" trip-distance / mean-car-speed * 60
  table:put destination-table "bike-time" trip-distance / mean-bike-speed * 60
  table:put destination-table "public-transport-time" trip-distance / mean-public-transport-speed * 60
  table:put destination-table "car-costs" trip-distance * car-costs-per-km
  table:put destination-table "public-transport-costs" trip-distance * public-transport-costs-per-km
  table:put destination-table "shared-car-transport-costs" trip-distance * shared-car-costs-per-km + trip-length * shared-car-costs-per-hour

  report destination-table
end
