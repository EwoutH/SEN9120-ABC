;; ##### DAILY FUNCTIONS ####
to start-trip [work?]
  let destination 0
  ifelse work?
    [set destination one-of work-destinations]
    [set destination one-of other-destinations]

  ;; Check if own car is available
  let available-modalities ["bike" "public-transport"]
  if owns-car? [
    set available-modalities lput "car" available-modalities  
    ;; TODO: Also check household cars
    ;; This is complicated, because first all household members need to check if they need their own car,
    ;; and only after the not used cars can be distributed among other household members (people will generally use their own car first)
  ]
  ;; Check if shared-car is available
  if any? cars with [shared? and not in-use?] [
    set available-modalities lput "shared-car" available-modalities 
  ]  

  ;; Choose modality based on a concious choice or previous choice, if previous choice is available
  let last-modality table:get destination "last-modality"

  let new-choice? not member? last-modality available-modalities or random-float 1 < (1 / max list 1 table:get destination "consecutive-count")
  let modality ""

  ;; Create a list with the effective costs for each available modality
  let eff-costs []
  foreach available-modalities [
    [key] ->
      let modifier 1
      if key = "shared-car" and shared-car-subscription [set modifier 0.75]    ;; 25% discount on all trips
      if key = "public-transport" and public-transport-subscription [
        ifelse work? 
          [set modifier 0.8]  ;; 20% discount in rush-hour (work trips)
          [set modifier 0.6]  ;; 40% discount otherwise (other trips)
      ]
      set eff-costs lput (table:get destination (word "eff-" key "-costs") * modifier) eff-costs 
  ]

  ;; Update the car and shared-car costs with subscriptions and the walking distance
  if member? "car" available-modalities [
    let walking-costs distance car-nr * patch-distance / walking-speed * value-of-time
    set eff-costs replace-item 2 eff-costs (item 2 eff-costs + walking-costs)
  ]
  if member? "shared-car" available-modalities [
    let walking-costs distance min-one-of cars with [shared? and not in-use?] [distance myself] * patch-distance / walking-speed * value-of-time
    ifelse member? "car" available-modalities   ;; The shared car is the fourth (3) item if a car is also available, otherwise the third (2) item
     [set eff-costs replace-item 3 eff-costs (item 3 eff-costs + walking-costs)]
     [set eff-costs replace-item 2 eff-costs (item 2 eff-costs + walking-costs)]
  ]

  let max-costs max eff-costs
  let min-costs min eff-costs

  ifelse member? "car" available-modalities [
    set min-monthly-costs-car min-monthly-costs-car + min-costs
    let min-costs-without-car min remove-item 2 eff-costs
    set min-monthly-costs min-monthly-costs + min-costs-without-car
  ] [
    set min-monthly-costs min-monthly-costs + min-costs
    let costs-with-car table:get destination "eff-car-costs"
    set min-monthly-costs-car min-monthly-costs-car + min list min-costs costs-with-car  ;; Note walking costs are ignored here, since they can't be calculated without a car location
  ]

  ifelse new-choice? [
    ;; Calculate utility for each modality based on where cars are parked, and normalize to 0 to 1 range (1 is zero costs, 0 is the maximum costs)
    let utility []
    foreach eff-costs [
      [key] -> set utility lput ((max-costs - key + min-costs) / max-costs) utility
    ]

    ;; Calculate preference for each modality based on preference, utility and preference-utility-tradeoff:
    ;;     preference = utility * preference-utility-tradeoff + preference * (1 - preference-utility-tradeoff)
    let i 0
    let preferences table:make
    foreach available-modalities [
      [key] -> table:put preferences key (item i utility * preference-utility-tradeoff + table:get modality-preference key * (1 - preference-utility-tradeoff))
      set i i + 1
    ]
    let max-preference max table:values preferences

    foreach available-modalities [
      [key] -> if table:get preferences key = max-preference [set modality key]
    ]
  ]
  [
    set modality last-modality 
  ]

  ;; Move car
  if modality = "car" [
    ask car-nr [
      set in-use? true
      drive-car table:get virtual-locations modality
    ]
  ]
  ;; Take shared car
  if modality = "shared-car" [
    ask min-one-of cars with [not in-use? and shared?] [distance myself] [
      set in-use? true
      set current-driver myself
      drive-car table:get virtual-locations modality
    ]
  ]  
  
  ;; Update destination table with the last modality
  ifelse modality = table:get destination "last-modality"
    [table:put destination "consecutive-count" table:get destination "consecutive-count" + 1]
    [table:put destination "last-modality" modality
     table:put destination "consecutive-count" 1]
  
  ;; Update counter table
  table:put modality-counter modality table:get modality-counter modality + 1

  set away? true
end


to end-trip
  ;; TODO:
  ;; - Add costs to months-costs counter
  ;; - Update preference somehow
  ;; - Save this modality as last used and increase a counter wiht +1 how many times this modality was used
  ;; - Move turtle (and car) back
  if owns-car? [ask car-nr [if in-use? [
    set in-use? false
    park-car
  ]]]
  ask cars with [shared? and in-use? and current-driver = myself] [
    set in-use? false
    set current-driver nobody
    park-car
  ]
  set away? false
end

to drive-car [target-color]
  let current-spot min-one-of spots [distance myself]
  ifelse shared? [
    ask current-spot [
      ifelse shared-occupancy > 0
        [set shared-occupancy shared-occupancy - 1]
        [set occupancy occupancy - 1]
    ]
  ] [
    ask current-spot [set occupancy occupancy - 1]
  ]
  move-to one-of patches with [pcolor = target-color]
end

to park-car
  ifelse shared? [
    let target-spot nobody
    ifelse only-park-designated-spots?
      [set target-spot min-one-of spots with [shared-occupancy < shared-capacity] [distance myself]] ;; TODO: When not parking on designated spots, they park top left. Give them a home spot of some kind
      [set target-spot min-one-of spots with [shared-occupancy < shared-capacity or occupancy < capacity] [distance myself]]
    move-to target-spot
    ask target-spot [ifelse shared-occupancy < shared-capacity
      [set shared-occupancy shared-occupancy + 1]
      [set occupancy occupancy + 1]
    ]
  ] [
    let target-spot min-one-of spots with [in-neighbourhood? and not private? and occupancy < capacity] [distance [owner] of myself]
    if target-spot = nobody [
      set target-spot one-of spots with [not in-neighbourhood?]
      ask owner [
        table:put modality-preference "car" table:get modality-preference "car" * (1 - preference-penalty-parking-outside-neighbourhood * 0.01)
      ]
    ]
    move-to target-spot
      ask target-spot [set occupancy occupancy + 1]
  ]
end


;; ##### MONTHLY FUNCTIONS ####

to update-destinations
  ;; TODO: This function can be used to extend the model by letting residents change up some of their destinations, by dropping, replacing and/or adding new destinations to their destinations lists
  ;; It can be called at go-monthly
end

to update-connections
  ;; TODO: This function can be used to extend the model by letting residents change up some of their connections, by dropping, replacing and/or adding new residents to their neighbours-contacts and parent-contacts lists
  ;; It can be called at go-monthly
end

to buy-sell-subscriptions
  ;; Buy or sell shared car subscription 
  if random-float 1 < subscription-monthly-buy-sell-chance / 100 [    ;; Chance to reconsider shared-car subscription this month
    let shared-car-trips table:get modality-counter "shared-car"
    ifelse shared-car-subscription
      [if shared-car-trips <= 1 [set shared-car-subscription false]]  ;; Sell if 1 or less trips are done this month
      [if shared-car-trips >= 4 [set shared-car-subscription true]]   ;; Buy if 4 or more trips are done this month
  ]
  ;; Buy or sell public tranport subscription 
  if random-float 1 < subscription-monthly-buy-sell-chance / 100 [                ;; Chance to reconsider public-transport subscription this month
    let public-transport-trips table:get modality-counter "public-transport"
    ifelse public-transport-subscription
      [if public-transport-trips <= 1 [set public-transport-subscription false]]  ;; Sell if 1 or less trips are done this month
      [if public-transport-trips >= 4 [set public-transport-subscription true]]   ;; Buy if 4 or more trips are done this month
  ]
  ;; TODO: Make time compressions (of days in a month) working by normalizing the threshold values
end

;; Function for selling and buying cars, the chance of selling or buying a car is determined by the ownership of car first, then the function checks if costs can be saved by choosing 
;; an alternative mode of transport, this difference is divided by the total costs and multiplied by a tradeoff. Then the difference in modality preference is added.
to buy-sell-car 
  set total-car-costs min-monthly-costs-car + (fixed-car-costs + parking-permit-costs) / 31 * days-in-month
  set total-costs min-monthly-costs
  let non-car-preference mean remove-item 0 table:values modality-preference 
  
  ifelse owns-car? [
    if total-car-costs > total-costs [
      ;; consider selling car
      let sell-car-chance ((total-car-costs - total-costs) / total-car-costs * preference-utility-tradeoff) + (non-car-preference - table:get modality-preference "car") * (1 - preference-utility-tradeoff)
        if random-float 1 < sell-car-chance [
          set owns-car? false
          ask cars with [owner = myself][
            drive-car table:get virtual-locations "car"
            die
          ]
        ]
    ]
  ] [
    if total-car-costs < total-costs [
      ;; consider buying car
      let buy-car-chance ((total-costs - total-car-costs) / total-costs * preference-utility-tradeoff) + (table:get modality-preference "car" - non-car-preference) * (1 - preference-utility-tradeoff)
        if random-float 1 < buy-car-chance [
          set owns-car? true
          hatch-cars 1 [set-car-properties false]
          set car-nr one-of cars with [owner = myself]
        ]
    ]
  ]
  set min-monthly-costs 0
  set min-monthly-costs-car 0
end

to update-preferences [contact-preferences]
  let alpha social-adoption-multiplier * adoption-speed
  foreach (table:keys modality-preference) [
    [key] -> table:put modality-preference key (table:get modality-preference key * (1 - alpha) + table:get contact-preferences key * alpha)
  ]
end


;; ##### YEARLY FUNCTIONS ####

to age-resident
  set age age + 1
end

to move-households
  let new-household-locations []
  ask households [
    if random-float 1 < chance-of-household-moving / 100 [
      let loc [list xcor ycor] of self
      set new-household-locations lput loc new-household-locations

      ask residents with [household-nr = myself][
        ask cars with [owner = myself][
          ask spots-here [set occupancy occupancy - 1] ;; TODO: Create function to (re)move cars
          die
        ]
        die
      ]
      die
    ]
  ]
  foreach new-household-locations setup-household
end

to move-out-child
  ask residents with [not parent? and age >= 18] [
    if random-float 1 < chance-of-moving-out / 100 [
      die
    ]
  ]
end

to update-labels
  ask spots [ifelse shared-capacity = 0
    [set label (capacity - occupancy)]
    [set label (word (capacity - occupancy) "-" (shared-capacity - shared-occupancy))]
  ]
end

to update-metrics
  set monthly-car-trips sum [table:get modality-counter "car"] of residents
  set monthly-shared-car-trips sum [table:get modality-counter "shared-car"] of residents
  set monthly-bike-trips sum [table:get modality-counter "bike"] of residents
  set monthly-public-transport-trips sum [table:get modality-counter "public-transport"] of residents
  
  set shared-car-subscriptions count residents with [shared-car-subscription]
  set public-transport-subscriptions count residents with [public-transport-subscription]
  
  set mean-car-preference mean [table:get modality-preference "car"] of residents
end
